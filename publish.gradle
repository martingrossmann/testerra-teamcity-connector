/**
 * Maven publishing configuration
 * */

// Maven pom.xml attributes
ext {
    libraryName = 'Testerra TeamCity Connector'
    artifact = project.getName().toLowerCase()
    packagingType = 'jar'

    libraryDescription = "Testerra ${project.getName()} module"

    siteUrl = 'https://testerra.io'
    gitUrl = 'scm:git:git://github.com/telekom/testerra-teamcity-connector.git'
    issueUrl = 'https://github.com/telekom/testerra-teamcity-connector/issues'

    developerId = 'MMS'
    developerName = 'Testerra Team T-Systems MMS'
    developerEmail = 'testerra@t-systems-mms.com'
    developerOrganization = 'T-Systems MMS'
    developerOrganizationUrl = 'https://www.t-systems-mms.com/'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

allprojects {

    publishing {

        task sourcesJar(type: Jar, dependsOn: classes) {
            archiveClassifier.set('sources')
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            archiveClassifier.set('javadoc')
            from javadoc
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact javadocJar
                artifact sourcesJar

                // Custom pom
                pom {
                    packaging = packagingType
                    groupId = group
                    artifactId = artifact

                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }

                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                            organization = developerOrganization
                            organizationUrl = developerOrganizationUrl
                        }
                    }

                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }

        repositories {
            maven {
                url System.getProperty("deployUrl", "none")
                credentials {
                    username System.getProperty("deployUsername", "none")
                    password System.getProperty("deployPassword", "none")
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }

    }
}
